
let AllowedExtensions = externaldata(ExtensionName:string, ExtensionId:string)
[
h@"https://raw.githubusercontent.com/Kilroy1337/usecases/main/MITRE%20ATT%26CK/Persistence/T1176%20-%20Browser%20Extensions/General%20browser%20extensions.csv",
h@"https://raw.githubusercontent.com/Kilroy1337/usecases/main/MITRE%20ATT%26CK/Persistence/T1176%20-%20Browser%20Extensions/VPN%20browser%20extensions.csv"
]
with(format='csv');
DeviceTvmBrowserExtensions
| join kind=leftanti (AllowedExtensions | project ExtensionId = tolower(ExtensionId), ExtensionName) on ExtensionId
| join DeviceInfo on DeviceId
| where IsActivated == "true"
| where ExtensionRisk in ("High", "Critical")
| where MachineGroup !in ("All Windows Workstations")
| summarize by ExtensionId, ExtensionName, ExtensionRisk, DeviceName, MachineGroup, BrowserName, ExtensionDescription
| sort by ExtensionRisk asc, ExtensionName asc;
